set nocompatible                                             " Treure la compatibilitat amb Vi

filetype off
filetype plugin indent on
filetype plugin on

packadd minpac
call minpac#init()

call minpac#add('ctrlpvim/ctrlp.vim')
call minpac#add('sjl/gundo.vim')
"?call minpac#add('gorkunov/smartpairs.vim')
call minpac#add('Lokaltog/vim-easymotion')
call minpac#add('tpope/vim-fugitive')
call minpac#add('kablamo/vim-git-log')
call minpac#add('xolox/vim-misc')
call minpac#add('tpope/vim-repeat')
call minpac#add('xolox/vim-session')
call minpac#add('mhinz/vim-signify')
call minpac#add('tpope/vim-surround')
call minpac#add('kana/vim-textobj-user')
call minpac#add('christoomey/vim-tmux-navigator')
call minpac#add('tpope/vim-unimpaired')
call minpac#add('pbrisbin/vim-mkdir')
call minpac#add('machakann/vim-highlightedyank')
"?call minpac#add('mitsuhiko/vim-jinja')
call minpac#add('joonty/vim-taggatron')
call minpac#add('lifepillar/vim-solarized8')
call minpac#add('adelarsq/vim-matchit')
call minpac#add('vim-airline/vim-airline')
call minpac#add('vim-airline/vim-airline-themes')
call minpac#add('k-takata/minpac', {'type':'opt'})

command! PackUpdate call minpac#update()
command! PackClean call minpac#clean()

"runtime macros/matchit.vim

filetype on

""""""""""""""""""""""""""""""""""""""""""""""""""
" Funcions
"""""""""""""""""""""""""""""""""""""""""""""""""""
function! EnsureDirExists (dir)
" Funció per crear directoris si no existeixen
    if !isdirectory(a:dir)
        if exists('*mkdir')
            call mkdir(a:dir,'p')
        endif
    endif
endfunc

function! DeleteTrailingWS()
" Funció per esborrar els espais a final de línia del buffer
    let previous_search=@/
    let previous_cursor_line=line('.')
    let previous_cursor_column=col('.')
    %s/\s\+$//ge
    let @/=previous_search
    call cursor(previous_cursor_line, previous_cursor_column)
endfunc

function! RetabIndents()
" Funció que canvia espais per tabs al principi de línia del buffer
    let previous_search=@/
    let previous_cursor_line=line('.')
    let previous_cursor_column=col('.')
    if &expandtab
        execute '%s@^\(\t\)\+@\=repeat(repeat(" ", '.&ts.'), len(submatch(0)))@e'
    else
        execute '%s@^\(\ \{'.&ts.'\}\)\+@\=repeat("\t", len(submatch(0))/'.&ts.')@e'
    endif
    let @/=previous_search
    call cursor(previous_cursor_line, previous_cursor_column)
endfunc

" Use Q to intelligently close a window
" (if there are multiple windows into the same buffer)
" or kill the buffer entirely if it's the last window looking into that buffer
function! CloseWindowOrKillBuffer()
    let number_of_windows_to_this_buffer = len(filter(range(1, winnr('$')), "winbufnr(v:val) == bufnr('%')"))

    " We should never bdelete a nerd tree
    if matchstr(expand("%"), 'NERD') == 'NERD'
        wincmd c
        return
    endif

    if number_of_windows_to_this_buffer > 1
        wincmd c
    else
        bdelete
    endif
endfunction

command! -nargs=0 -bar Qargs execute 'args' QuickfixFilenames()
function! QuickfixFilenames()
  " Building a hash ensures we get each buffer only once
  let buffer_numbers = {}
  for quickfix_item in getqflist()
    let buffer_numbers[quickfix_item['bufnr']] = bufname(quickfix_item['bufnr'])
  endfor
  return join(map(values(buffer_numbers), 'fnameescape(v:val)'))
endfunction


""""""""""""""""""""""""""""""""""""""""""""""""""
" Creació de directoris
""""""""""""""""""""""""""""""""""""""""""""""""""
:silent call EnsureDirExists($HOME.'/.vim_backup')           " Crear directori de backup
" :silent call EnsureDirExists($HOME.'/.vim_swap')             " Crear directori de swap
:silent call EnsureDirExists($HOME.'/.vim_undo')             " Crear directori de undo
:silent call EnsureDirExists($HOME.'/.vim_view')             " Crear directori de view
:silent call EnsureDirExists($HOME.'/.vim_sessions')         " Crear directori de sessions


""""""""""""""""""""""""""""""""""""""""""""""""""
" General
""""""""""""""""""""""""""""""""""""""""""""""""""

" Mantenir la posició del cursor quan obres un buffer al mateix lloc a on el tenies al tancar-lo
autocmd BufWinLeave *.* silent! mkview
autocmd BufWinEnter *.* silent! loadview
autocmd FocusLost *.* :wa

" When restoring a hidden buffer Vim doesn't always keep the same view (like
" when your view shows beyond the end of the file). (Vim tip 1375)
if ! &diff
    au BufLeave * let b:winview = winsaveview()
    au BufEnter * if(exists('b:winview')) | call winrestview(b:winview) | endif
endif

" Posar al títol de la finestra el path del buffer que estem editant
let &titlestring = expand('%:pwd')
if &term == 'screen' || &term == 'xterm'
    set title
endif

" keyboard shortcuts
let mapleader = " "

set autoindent                                               " Activar autoindendació
set autoread                                                 " Reload files when changed on disk, i.e. via `git checkout`
set backspace=2                                              " Fix broken backspace in some setups
set backupdir=~/.vim_backup                                  " Canviar directori de buffer de backup
if has('mac')
    set clipboard=unnamed                                    " Yank and paste with the system clipboard
elseif has('unix')
    set clipboard=unnamedplus                                " Yank and paste with the system clipboard
endif
set cursorline                                               " Ressalta la línia a on està el cursor
" set directory=~/.vim_swap                                    " Canviar directori de fitxers de swap
set encoding=utf-8                                           " Posar utf-8 com a encoding per defecte
set expandtab tabstop=4 softtabstop=4 shiftwidth=4           " Definir els tabs com 4 espais
set foldlevel=99
set foldmethod=indent                                        " Es creen folds directament amb la indentació
set foldnestmax=1                                            " Perquè es crein folds només al primer nivell d'indentació
"set guifont=DejaVu\ Sans\ Mono\ for\ Powerline:h9
set hidden                                                   " Permet moure entre buffers encara que aquests estiguin editats sense guardar
set history=10000                                            " Guarda les 10000 últimes comandes
set hlsearch                                                 " Es ressalten les coincidencies de la cerca
set ignorecase                                               " Case-sensitive search if any caps
set incsearch                                                " Search as you type
set infercase                                                " Per fer case sensitive l'autocomplete
set iskeyword+=_,@                                           " Not word dividers
set laststatus=2                                             " Always show statusline
set lazyredraw                                               " No repinta la pantalla mentre s'executen macros, més ràpid
set linebreak
set nolist                                                   " No ressaltar els caràcters especials
set listchars=tab:▸\ ,trail:▫,eol:↵,extends:»,precedes:«     " Llistat de caràcters especials que es ressalten
set mouse=                                                   " Deshabilitar el ratolí
set noerrorbells                                             " No vull errors sonors
set visualbell                                               " No vull errors visuals
set nowrap sidescroll=1 sidescrolloff=1                      " No tallar frases
set noswapfile
set nrformats=                                               " Per utilitzar el sistema decimal, no octal a l'incrementar o decrementar números
set number                                                   " Show line numbers
set relativenumber                                           " Shows relative numbers
set ruler                                                    " Show where you are
set showcmd                                                  " Mostra els parcials de les comandes
set showtabline=2                                            " Mostra la barra de tabs/buffers sempre
set smartcase                                                " Case-sensitive search if any caps
set splitright splitbelow                                    " Obre les noves finestres a la dreta i a sota
set switchbuf=usetab                                         " A l'obrir un nou fitxer posiciona a la tab/finestra que el tingui, no l'obre de nou
set tags=tags                                                " Afegir tags del virtualenv als tags per defecte
set undodir=~/.vim_undo                                      " Canviar directori de fitxers de undo
set undofile                                                 " Undo persistent, encara que es tanqui el buffer
set undolevels=10000                                         " Augmenta a 10000 el nivel de undo
set undoreload=10000                                         " Augmenta a 10000 el nivel de redo
set viewdir=~/.vim_view                                      " Canviar directori de fitxers de view
set wildignore=*/tmp/,*/.git/*,*.so,*.swp,*.zip,*.pdf,*.bak,*.pyc,*.pyo,*.class,*.tmp,*~      " Ignorar aquests tipus de fitxers
set wildmenu                                                 " Show a navigable menu for tab completion on command line
set wildmode=full                                            " Mostra les possibilitats d'una comanda rotllo zsh
if !has('nvim')
    set term=screen-256color
endif
set pastetoggle=<f5>

autocmd BufNewFile,BufRead *.h,*.m set tags+=~/Documents/global-objc-tags
" autocmd BufNewFile,BufRead *.js set tags+=.js.tags
autocmd BufNewFile,BufRead *.php set tags+=.php.tags
autocmd BufNewFile,BufRead *.css set tags+=.css.tags
" autocmd BufNewFile,BufRead *.twig set tags+=.js.tags;.css.tags
autocmd BufNewFile,BufRead *.twig set tags+=.css.tags

" Activar colors al text
syntax on

set t_Co=256                                                 " Número de colors
let base16colorspace=256
set background=light
colorscheme solarized8
highlight SpecialKey ctermbg=NONE guibg=NONE
if !has('gui_running')
    highlight Normal ctermbg=NONE guibg=NONE
endif
set termguicolors

" set Vim-specific sequences for RGB colors
let &t_8f = "\<Esc>[38;2;%lu;%lu;%lum"
let &t_8b = "\<Esc>[48;2;%lu;%lu;%lum"


""""""""""""""""""""""""""""""""""""""""""""""""""
" Mapeig de teclat en mode inserció
""""""""""""""""""""""""""""""""""""""""""""""""""

" Anar al mode normal des del mode insert amb jj
inoremap jj <Esc>


""""""""""""""""""""""""""""""""""""""""""""""""""
" Mapeig de teclat en mode visual
""""""""""""""""""""""""""""""""""""""""""""""""""



""""""""""""""""""""""""""""""""""""""""""""""""""
" Mapeig de teclat en mode comanda
""""""""""""""""""""""""""""""""""""""""""""""""""

" Escriu a la línia de comandes el directori actual posant %%
cnoremap %% <C-R>=fnameescape(expand('%:h')).'/'<Cr>

" Permet moure's per la història de comandes amb <C-n> i <C-p>
cnoremap <C-p> <Up>
cnoremap <C-n> <Down>

cmap w!! w !sudo tee % >/dev/null


""""""""""""""""""""""""""""""""""""""""""""""""""
" Mapeig de comandes
""""""""""""""""""""""""""""""""""""""""""""""""""

" Si escribim malament el guardar i/o sortir de vim, que funcioni
command! WQ wq
command! Wq wq
command! W w
command! Q q

command! Qa qa


""""""""""""""""""""""""""""""""""""""""""""""""""
" Mapeig de teclat en mode normal
""""""""""""""""""""""""""""""""""""""""""""""""""

" Deshabilitar les tecles de moviment
nnoremap <Left> :echoe "Use h"<CR>
nnoremap <Right> :echoe "Use l"<CR>
nnoremap <Up> :echoe "Use k"<CR>
nnoremap <Down> :echoe "Use j"<CR>

" Tracta les línies llarques com a línies separades
map j gj
map k gk

" Moure a la finestra dreta amb Ctrl-H
nmap <C-h> <C-w>h
" Moure a la finestra inferior amb Ctrl-J
nmap <C-j> <C-w>j
" Moure a la finestra superior amb Ctrl-K
nmap <C-k> <C-w>k
" Moure a la finestra esquerra amb Ctrl-L
nmap <C-l> <C-w>l

map gb :bnext<Cr>
map gB :bprevious<Cr>

" Per fer redo es pot amb <C-r> i amb U
nnoremap U <C-r>
nnoremap :: @:

" Per centrar les cerques a vim
nnoremap n nzz
nnoremap N Nzz

" Executar vimgrep al directori actual
nmap <Leader>vg :vimgrep //gj **/*<Left><Left><Left><Left><Left><Left><Left><Left>

" Mostrar una finestra amb els resultats de la cerca prèvia al buffer actual
nnoremap <silent> <Leader>/ :execute 'vimgrep /'.@/.'/g %'<Cr>:copen<Cr>

" Copiar del caràcter actual al final de línia amb la tecla Y, per similitud amb D i C
nmap Y y$

" Separar línies amb K
nnoremap K i<Cr><Esc>^

" Guardar el buffer actual amb <Leader>w
nnoremap <Leader>w :w<CR>
" Guardar el buffer actual i tancar-lo amb <Leader>W
nnoremap <Leader>W :wq<CR>

" Posar la configuració de tabs a 2 espais amb <Leader><Leader>2
nnoremap <Leader><Leader>2 <Esc>:set noexpandtab tabstop=2 softtabstop=2 shiftwidth=2<Cr>
" Posar la configuració de tabs a 4 espais amb <Leader><Leader>4
nnoremap <Leader><Leader>4 <Esc>:set noexpandtab tabstop=4 softtabstop=4 shiftwidth=4<Cr>
" Posar la configuració de tabs a 8 espais amb <Leader><Leader>8
nnoremap <Leader><Leader>8 <Esc>:set noexpandtab tabstop=8 softtabstop=8 shiftwidth=8<Cr>
" Posar la configuració de 4 espais amb <leader><leader>0
nnoremap <Leader><Leader>0 <Esc>:set expandtab tabstop=4 softtabstop=4 shiftwidth=4<Cr>

" Substituir la paraula sota el cursor a tot el buffer
nnoremap <Leader>* :%s/\<<C-r><C-w>\>//g<Left><Left>

" Obrir .vimrc a un nou tab amb <Leader>vv
nnoremap <Leader>vv :e $MYVIMRC<Cr>

" Obre tots els buffers oberts en tabs amb <Leader>tb
nnoremap <silent> <Leader>tb :tab :ball<Cr>
" Tancar la pestanya actual amb <Leader>tc
nnoremap <silent> <Leader>tc :tabclose<Cr>
" Obrir el buffer actual a una nova pestanya amb <Leader>te
nnoremap <silent> <Leader>te :tabedit %<Cr>
" Tancar totes les pestanyes menys l'actual amb <Leader>to
nnoremap <silent> <Leader>to :tabonly<Cr>

" Mostrar el tipus de fitxer
nnoremap <Leader>ty :set filetype<Cr>

" Tancar tots els buffers que no estan oberts a cap finestra ni pestanya
nnoremap <silent> dhb :Wipeout<Cr>

" Utilitzar Q per tancar buffers
nnoremap <silent> Q :call CloseWindowOrKillBuffer()<Cr>

" Per seleccionar l'últim text enganxat
nnoremap <expr> gv '`[' . strpart(getregtype(), 0, 1) . '`]'

" Intercanviar v per <C-v>: v per defecte serà en bloc
nnoremap v <C-V>
nnoremap <C-V> v

map <Leader>e :e <C-R>=expand('%:p:h') . '/' <Cr>
map <Leader>c :f <C-R>=expand('%:p:h') . '/' <Cr>
map <Leader>t :tabe <C-R>=expand('%:p:h') . '/' <Cr>
map <Leader>s :split <C-R>=expand('%:p:h') . '/' <Cr>

map <Leader>n :enew<Cr>i

" <C-k> i <C-j> mouen la finestra amunt i avall una línia alhora que el cursor
map <c-j> j<c-e>
map <c-k> k<c-y>

" Esborra els espais al final de totes les línies amb <Leader>dt
noremap <silent> <Leader>dt :call DeleteTrailingWS()<Cr>
" Canvia els espais per tabuladors al principi de totes les línies amb <Leader>ri
noremap <silent> <Leader>ri :call RetabIndents()<Cr>

" Repeteix la última cerca amb els mateixos flags amb & en mode normal, visual o de selecció
nnoremap & :&&<CR>
xnoremap & :&&<CR>

augroup CursorLineOnlyInActiveWindow
    autocmd!
    autocmd VimEnter,WinEnter,BufWinEnter * setlocal cursorline
    autocmd WinLeave * setlocal nocursorline
augroup END

if has("eval")
    function! SL(function)
        if exists('*'.a:function)
            return call(a:function,[])
        else
            return ''
        endif
    endfunction
endif

"set statusline=[%n]\ %<%.99f\ %h%w%m%r%{SL('CapsLockStatusline')}%y%{SL('fugitive#statusline')}%#ErrorMsg#%{SL('SyntasticStatuslineFlag')}%*%=%-14.(%l,%c%V%)\ %P
"set statusline=[%n]\ %<%.99f\ %h%w%m%r%y%{SL('fugitive#statusline')}%#ErrorMsg#%{SL('SyntasticStatuslineFlag')}%*%=%(%l,%c%V%)

""""""""""""""""""""""""""""""""""""""""""""""""""
" Configuració de plugins
""""""""""""""""""""""""""""""""""""""""""""""""""

" CtrlP: Navegació ràpida de fitxers i buffers.
" git clone https://github.com/kien/ctrlp.vim

let g:ctrlp_match_window = 'order:ttb,max:30'                " Ordenació i número màxim de resultats
let g:ctrlp_regexp = 1                                       " Fa servir cerca amb regex, no fuzzy
let g:ctrlp_max_files=0
let g:ctrlp_working_path_mode = 'w'

if executable('ag')
    let g:ackprg = 'ag --nogroup --nocolor --column'

    " Use Ag over Grep
    set grepprg=ag\ --nogroup\ --nocolor\ --column
    set grepformat=%f:%l:%c:%m

    " Use ag in CtrlP for listing files. Lightning fast and respects .gitignore
    let g:ctrlp_user_command = 'ag %s -lU --nogroup --nocolor --column -g ""'
endif

" Mostrar finestra amb el CtrlP de buffers oberts
nmap <Leader>b :CtrlPBuffer<Cr>
" Mostrar finestra amb el CtrlP de fitxers
nmap <Leader>f :CtrlP<Cr>
" Mostrar finestra amb el CtrlP de fitxers escanejant de nou
nmap <Leader>F :CtrlPClearCache<Cr>:CtrlP<Cr>
" Mostrar finestra amb els tags extrets fent ctags
nmap <Leader>. :CtrlPClearCache<Cr>:CtrlPTag<Cr>

" Vim-easymotion: Per saltar entre paraules, cerques, etc. de manera més àgil.
" git clone https://github.com/Lokaltog/vim-easymotion

" Vim-fugitive: Utilitzar Git des de Vim
" git clone https://github.com/tpope/vim-fugitive

" Vim-git-log: Amplia fugitive. Per veure el log dels canvis al repositori.
" git clone https://github.com/kablamo/vim-git-log

" Vim-signify: Marca els canvis del fitxer respecte el repositori a la columna esquerra del fitxer.
" git clone https://github.com/mhinz/vim-signify

" Gundo: Mostra l'arbre de modificacions d'un fitxer
" git clone https://github.com/sjl/gundo.vim
nnoremap <Leader>u :GundoToggle<Cr>

" vim-session: Gestionar les sessions de vim
" Necessita vim-misc: git clone https://github.com/xolox/vim-misc
" git clone https://github.com/xolox/vim-session
let g:session_directory = '~/.vim_sessions'
let g:session_autoload = 'yes'
let g:session_autosave = 'yes'
let g:session_command_aliases = 1
nnoremap <Leader>so :OpenSession 
nnoremap <Leader>ss :SaveSession 
nnoremap <Leader>sd :DeleteSession<Cr>
nnoremap <Leader>sc :CloseSession<Cr>

" vim-surround: mitjançant l'acció 's' es posen i treuen parentesis, claudators, cometes simples, un tag...
" git clone https://github.com/tpope/vim-surround

" vim-unimpaired:
" git clone https://github.com/tpope/vim-unimpaired

" vim-repeat:
" git clone https://github.com/tpope/vim-repeat

" vundle:
" git clone https://github.com/gmarik/vundle.git

" vim-tmux-navigator: Per passar de vim a tmux
" git clone https://github.com/christoomey/vim-tmux-navigator

" vim-misc:
" git clone https://github.com/xolox/vim-misc

" vim-taggatron
let g:tagcommands = {
\    "php" : {
\        "tagfile": ".php.tags",
\        "cmd" : "ctags",
\        "args": "-R"
\    },
\    "css" : {
\        "tagfile": ".css.tags",
\        "cmd" : "ctags",
\        "args": "-R"
\    },
\}
" \    "javascript" : {
" \        "tagfile": ".js.tags",
" \        "cmd" : "ctags",
" \        "args": "-R"
" \    },

au BufRead,BufNewFile *.swift set filetype=swift

au BufRead,BufNewFile *.twig set filetype=jinja
au BufRead,BufNewFile *.twig set filetype=htmljinja
au BufRead,BufNewFile *.html set filetype=jinja
au BufRead,BufNewFile *.html set filetype=htmljinja

" vim-airline: Línia de status i de buffers
" git clone https://github.com/bling/vim-airline
"let g:airline_section_b = '%{getcwd()}'
let g:airline_section_c = '%f'
"let g:airline_section_x = ''
let g:airline_section_y = ''
let g:airline#extensions#tagbar#enabled = 0
let g:airline#extensions#hunks#enabled = 0
let g:airline#extensions#virtualenv#enabled = 0
let g:airline_left_sep=''
let g:airline_right_sep=''
let g:airline#extensions#tabline#show_buffers = 1
let g:airline#extensions#tabline#enabled = 1
let g:airline#extensions#tabline#formatter = 'default'
let g:airline#extensions#tabline#buffer_nr_show = 1
let g:airline#extensions#bufferline#enabled = 1
let g:airline#extensions#whitespace#checks = []

